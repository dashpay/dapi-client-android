apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

buildscript {
    ext.kotlin_version = '1.3.50'
    ext.dashj_version = '0.18-SNAPSHOT'
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.8"
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

group 'org.dashevo'
version '0.14-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.11.4' }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.28.0' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }

            task.plugins {
                grpc { option 'lite' }
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'org.dashevo:dpp:0.14-SNAPSHOT'
    implementation "org.dashj:dashj-core:$dashj_version"
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
    testImplementation 'org.assertj:assertj-core:3.10.0'
    testImplementation 'org.mockito:mockito-all:2.0.2-beta'
    testImplementation 'com.squareup.retrofit2:retrofit-mock:2.4.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.11.0'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-RC1"

     implementation 'io.grpc:grpc-okhttp:1.28.0' // CURRENT_GRPC_VERSION
    implementation('io.grpc:grpc-protobuf-lite:1.28.0') { // CURRENT_GRPC_VERSION
        exclude module: "protobuf-lite"
    }
    implementation 'io.grpc:grpc-stub:1.28.0' // CURRENT_GRPC_VERSION
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact sourcesJar
            from components.java
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

assemble.dependsOn(publishToMavenLocal)

test {
    useJUnitPlatform()
}